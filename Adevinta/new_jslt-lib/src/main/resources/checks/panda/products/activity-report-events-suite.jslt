import "lib/metrics.jslt" as m
import "checks/commons.jslt" as c
import "checks/panda/commons.jslt" as panda-commons
import "checks/panda/products/commons.jslt" as cp

// This suite will check events in order to use panda/activity_report product.
// Activity report product does stats on this type of events : 
// bump,delete,edit,favorite,lead,list,message,new,phone,reply,smartbump,sms,view 

def suite-name()
   "panda.products.activity_report"

let typeTag = c:taggify-event-type(.)
let eventTypeTag = m:tag("obj-type", $typeTag)

let providerTag = m:tag("provider", m:taggify(.provider."@id", "unknown", panda-commons:available-providers()))

// Create and Edit events
def isAdCreate(root)
    contains($typeTag, ["ad-create", "ad-edit"])

// Lifecycle and behaviors events
def isAdLifeCycleAndBehavior(root)
    contains($typeTag, ["ad-favorite", "ad-view", "ad-deletion", "ad-publish", "ad-unpublish", "ad-republish", "ad-update", "ad-save", "ad-unsave", "ad-update", "ad-bump", "ad-smartbump"])

// Listing events
def isAdListing(root)
    contains($typeTag, ["list-view"])

// Contact events 
def isAdContact(root)
    contains($typeTag, ["ui-element-engage", "ad-reply-submitted", "ad-phone-called", "ad-sms-app-opened", "ad-phone-displayed", "ad-reply-notification-content-submitted"])


if (isAdCreate(.)) 
    cp:check-generic-fields(., suite-name(), $eventTypeTag, $providerTag)
    +
    cp:check-classified-ad-events(., suite-name(), $eventTypeTag, $providerTag)
    +
    cp:check-additional-fields(., suite-name(), $eventTypeTag, $providerTag)
else if (isAdLifeCycleAndBehavior(.))
    cp:check-generic-fields(., suite-name(), $eventTypeTag, $providerTag)
    +
    cp:check-classified-ad-events(., suite-name(), $eventTypeTag, $providerTag)
else if (isAdListing(.))
    cp:check-generic-fields(., suite-name(), $eventTypeTag, $providerTag)
    +
    cp:check-listing-events(., suite-name(), $eventTypeTag, $providerTag)
else if (isAdContact(.))
    cp:check-generic-fields(., suite-name(), $eventTypeTag, $providerTag)
    +
    cp:check-contact-events(., suite-name(), $eventTypeTag, $providerTag)
else c:no-check()
